import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
}

loom {
    accessWidenerPath = file("src/main/resources/lithium.accesswidener")
    mixin.defaultRefmapName = "mixins.lithium.refmap.json"
}

apply plugin: 'fabric-loom'

group = project.maven_group
version = "${project.mcsr_version}+backport-${project.lithium_version}+${project.minecraft_version}"
base {
    archivesName = project.archives_base_name
}

sourceSets {
    api {
        java {
            compileClasspath += main.compileClasspath
        }
    }

    main {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
        }
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "dev.tildejustin:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    //modCompile "com.google.code.findbugs:jsr305:3.0.2"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

// Include the license in all JAR outputs
tasks.withType(Jar).configureEach {
    from "LICENSE.txt"
    from sourceSets.api.output
}

tasks.register('apiJar', Jar) {
    archiveClassifier.set "api-dev"

    from sourceSets.api.output
}

tasks.register('remapApiJar', RemapJarTask) {
    dependsOn apiJar
    archiveClassifier.set("api")

    input = apiJar.archiveFile.get().asFile
    addNestedDependencies = false
}

build.dependsOn remapApiJar

jar {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    from sourceSets.api.output.classesDirs
    from sourceSets.api.output.resourcesDir
}

publishing {
    publications {
        main (MavenPublication) {
            artifactId "lithium"
            artifact(remapJar)
        }

        api (MavenPublication) {
            artifactId "lithium-api"
            artifact(remapApiJar)
        }
    }
}

build.dependsOn remapApiJar
